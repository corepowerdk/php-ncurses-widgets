# Maintainer: Heine Pedersen <heine at corepower dot dk>
pkgname=php-ncurses-cp
pkgver=1.0.2
pkgrel=1
pkgdesc="Terminal screen handling and optimization package"
url="http://pecl.php.net/package/ncurses"
arch=('x86_64' 'i686')
license=('PHP')
depends=('php' 'ncurses')
backup=('etc/php/conf.d/ncurses.ini')
replaces=('php-ncurses')
conflicts=('php-ncurses')

source=(
    "http://pecl.php.net/get/ncurses-${pkgver}.tgz"
    'ncurses-php7-support.patch'
    'ncurses_fix_addstr.patch'
    'ncurses_fix_mousemask_warning.patch'
)

sha256sums=(
    '7fa4998d06a00a094276e5283fd6a408cbf78cd2d3b889fe6ab51be3780c9063'
    '24da58ba3126329d93701ebc68a0dc3993340883bf648006783bb4222c9a6b5c'
    '6b27c1ebc28dc8f6b3f279c28db0ee79aa1cea7fac9bedad724306169d66829b'
    '1d76f9c762bec89c54b3914225e90a16a15793b5b52d8002d115bcec564a52b9')

build() {
    phpver=$(php -r 'echo (int) phpversion();')
    cd "$srcdir/ncurses-$pkgver"

    if [ $phpver -ge 7 ]; then
        patch < ../../ncurses-php7-support.patch
        patch < ../../ncurses_fix_addstr.patch
        patch < ../../ncurses_fix_mousemask_warning.patch
    fi

    phpize
    ./configure --prefix=/usr --with-ncurses
    make
}

package() {
    cd "$srcdir/ncurses-$pkgver"

    make INSTALL_ROOT="$pkgdir" install
    echo 'extension=ncurses.so' > ncurses.ini 
    install -Dm644 ncurses.ini "$pkgdir/etc/php/conf.d/ncurses.ini"
}
